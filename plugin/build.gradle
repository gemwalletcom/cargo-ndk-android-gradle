plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'signing'
    id 'com.gradle.plugin-publish' version '1.2.1'
}

version = System.getenv("VER_NAME") ?: "0.5.1"
group = "com.gemwallet.gradle"

gradlePlugin {
    website = 'https://github.com/gemwalletcom/cargo-ndk-android-gradle'
    vcsUrl = 'https://github.com/gemwalletcom/cargo-ndk-android-gradle.git'
    plugins {
        cargoNdkAndroidPlugin {
            id = 'com.gemwallet.cargo-ndk'
            displayName = 'Cargo NDK Android Plugin'
            description = 'A plugin that helps build Rust JNI libraries via Cargo NDK for use in Android projects.'
            implementationClass = 'com.github.willir.rust.CargoNdkBuildPlugin'
            tags.set(['rust', 'cargo', 'cargo-ndk', 'android'])
        }
    }
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    compileOnly "com.android.tools.build:gradle:8.7.2"

    testImplementation platform("org.spockframework:spock-bom:2.3-groovy-3.0")
    testImplementation "org.spockframework:spock-core"
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/gemwalletcom/cargo-ndk-android-gradle")
            credentials {
                username = (System.getenv("GITHUB_ACTOR")
                        ?: System.getenv("GITHUB_USER")
                        ?: project.findProperty("github.user")) as String
                password = (System.getenv("GITHUB_TOKEN")
                        ?: project.findProperty("github.token")) as String
            }
        }
    }
    publications {
        pluginMaven(MavenPublication) {
            pom {
                name = 'Cargo NDK Android Plugin'
                description = 'A plugin that helps build Rust JNI libraries via Cargo NDK for use in Android projects.'
                url = 'https://github.com/gemwalletcom/cargo-ndk-android-gradle'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'gemwallet'
                        name = 'Gem Wallet'
                        email = 'ops@gemwallet.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/gemwalletcom/cargo-ndk-android-gradle.git'
                    developerConnection = 'scm:git:ssh://github.com/gemwalletcom/cargo-ndk-android-gradle.git'
                    url = 'https://github.com/gemwalletcom/cargo-ndk-android-gradle'
                }
            }
        }
    }
}

signing {
    def signingKey = System.getenv("MVN_SIGNING_KEY")
    def signingPassphrase = System.getenv("MVN_SIGNING_PASSPHRASE")
    if (signingKey && signingPassphrase) {
        useInMemoryPgpKeys(signingKey, signingPassphrase)
        sign publishing.publications
    }
}
