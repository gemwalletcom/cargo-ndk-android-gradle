plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'signing'
    id 'com.gradle.plugin-publish' version '2.0.0'
}

version = System.getenv("VER_NAME") ?: "0.5.3"
group = "com.gemwallet.gradle"

def groovyMajor = (GroovySystem.version.tokenize('.')[0] as Integer)
def spockGroovyVersion = groovyMajor >= 5 ? '5.0' : (groovyMajor >= 4 ? '4.0' : '3.0')

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

gradlePlugin {
    website = 'https://github.com/gemwalletcom/cargo-ndk-android-gradle'
    vcsUrl = 'https://github.com/gemwalletcom/cargo-ndk-android-gradle.git'
    plugins {
        cargoNdkAndroidPlugin {
            id = 'com.gemwallet.cargo-ndk'
            displayName = 'Cargo NDK Android Plugin'
            description = 'A plugin that helps build Rust JNI libraries via Cargo NDK for use in Android projects.'
            implementationClass = 'com.github.willir.rust.CargoNdkBuildPlugin'
            tags.set(['rust', 'cargo', 'cargo-ndk', 'android'])
        }
    }
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    compileOnly "com.android.tools.build:gradle:8.13.2"

    testImplementation platform("org.spockframework:spock-bom:2.4-groovy-${spockGroovyVersion}")
    testImplementation "org.spockframework:spock-core"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/gemwalletcom/cargo-ndk-android-gradle")
            credentials {
                username = (System.getenv("GITHUB_ACTOR")
                        ?: System.getenv("GITHUB_USER")
                        ?: project.findProperty("github.user")) as String
                password = (System.getenv("GITHUB_TOKEN")
                        ?: project.findProperty("github.token")) as String
            }
        }
    }
}

// Add POM metadata to all publications (including auto-generated marker publications)
afterEvaluate {
    publishing.publications.configureEach {
        pom {
            name = 'Cargo NDK Android Plugin'
            description = 'A plugin that helps build Rust JNI libraries via Cargo NDK for use in Android projects.'
            url = 'https://github.com/gemwalletcom/cargo-ndk-android-gradle'
            licenses {
                license {
                    name = 'MIT License'
                    url = 'https://opensource.org/licenses/MIT'
                }
            }
            developers {
                developer {
                    id = 'gemwallet'
                    name = 'Gem Wallet'
                    email = 'ops@gemwallet.com'
                }
            }
            scm {
                connection = 'scm:git:git://github.com/gemwalletcom/cargo-ndk-android-gradle.git'
                developerConnection = 'scm:git:ssh://github.com/gemwalletcom/cargo-ndk-android-gradle.git'
                url = 'https://github.com/gemwalletcom/cargo-ndk-android-gradle'
            }
        }
    }
}

signing {
    def signingKey = System.getenv("MVN_SIGNING_KEY")
    def signingPassphrase = System.getenv("MVN_SIGNING_PASSPHRASE")
    required = signingKey && signingPassphrase
    if (signingKey && signingPassphrase) {
        useInMemoryPgpKeys(signingKey, signingPassphrase)
    }
    sign publishing.publications
}
